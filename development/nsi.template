; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Unknown Horizons"
!define PRODUCT_VERSION "2009.0"
!define PRODUCT_PUBLISHER "The Unknown Horizons Team"
!define PRODUCT_WEB_SITE "http://www.unknown-horizons.org"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "gfx\uh.ico"
!define MUI_UNICON "gfx\uninstall.ico"
!define MUI_WELCOMEFINISHPAGE_BITMAP "gfx\banner-1.bmp"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_RIGHT
!define MUI_HEADERIMAGE_BITMAP "gfx\header-1.bmp"
!define UH_ICON "$INSTDIR\content\gfx\uh.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_FUNCTION run_uh
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------


Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup.exe"
InstallDir "$PROGRAMFILES\Unknown Horizons"
ShowInstDetails nevershow
ShowUnInstDetails nevershow
BrandingText "${PRODUCT_NAME} ${PRODUCT_VERSION} installer"

;--------- RUN Unknown Horizons -----
Function run_uh
  ExecShell "open" "$INSTDIR\run_uh.py" ;Unknown Horizons starten
FunctionEnd
;---------- OnInit --------------
Function .onInit
  SectionSetSize 1 35664 ;Python Sektion
FunctionEnd


Section "Unknown Horizons" uh
  SetOutPath "$INSTDIR"
  SetOverwrite try
%s
SectionEnd


;---------- DOWNLOAD PYTHON -------
Section "Python (required)" Python
  SetDetailsPrint textonly

  DetailPrint "Downloading Python"
  NSISdl::download http://www.python.org/ftp/python/2.5.2/python-2.5.2.msi $TEMP\pysetup.msi
  Pop $R0 ;Get the return value
    StrCmp $R0 "success" +3
      MessageBox MB_OK "Download failed: $R0"
      Quit

  DetailPrint "Installing Python"
  ExecWait '"msiexec" /i "$TEMP\pysetup.msi"'

  DetailPrint "Deleting Python installer"
  Delete $TEMP\pysetup.msi
SectionEnd
;----------- OPEN AL --------------
Section "OpenAL (required)" OpenAL
  SetDetailsPrint textonly

  SetOutPath "$TEMP"
  File "oalinst\oalinst.exe"
  DetailPrint "Installing OpenAL"
  ExecWait "$TEMP\oalinst.exe"

  DetailPrint "Deleting OpenAL installer"
  Delete "$TEMP\oalinst.exe"
SectionEnd
;--------- SECTION END ------------

Section -AdditionalIcons
  CreateDirectory "$SMPROGRAMS\Unknown Horizons"
  CreateShortCut "$SMPROGRAMS\Unknown Horizons\Unknown Horizons.lnk" "$INSTDIR\run_uh.py" "" "${UH_ICON}"
  CreateShortCut "$DESKTOP\Unknown Horizons.lnk" "$INSTDIR\run_uh.py" "" "${UH_ICON}"
  CreateShortCut "$SMPROGRAMS\Unknown Horizons\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) wurde erfolgreich deinstalliert."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Möchten Sie $(^Name) und alle seinen Komponenten deinstallieren?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\uninst.exe"
%s

  Delete "$SMPROGRAMS\Unknown Horizons\Uninstall.lnk"

  RMDir "$SMPROGRAMS\Unknown Horizons"
%s
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd


;Description
LangString DESC_uh ${LANG_ENGLISH} "Unknown Horizons - The game (including FIFE)"
LangString DESC_Python ${LANG_ENGLISH} "Python - Only if it's not already on your hard drive!"
LangString DESC_OpenAL ${LANG_ENGLISH} "OpenAL: Necessary for music and sound! Choose this one if it isn't installed already."

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${uh} $(DESC_uh)
  !insertmacro MUI_DESCRIPTION_TEXT ${Python} $(DESC_Python)
  !insertmacro MUI_DESCRIPTION_TEXT ${OpenAL} $(DESC_OpenAL)
!insertmacro MUI_FUNCTION_DESCRIPTION_END